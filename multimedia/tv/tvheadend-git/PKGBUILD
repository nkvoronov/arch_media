pkgname=tvheadend-git
_gitname='source-git'
pkgver=4.3.r1804.gebb096804
pkgrel=1
pkgdesc="TV streaming server for Linux"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://tvheadend.org/"
license=('GPL3')

license=('GPL3')
depends=('avahi' 'ffmpeg' 'libdvbcsa' 'libfdk-aac' 'libhdhomerun' 'openssl' 'pcre2' 'uriparser')
makedepends=('git' 'python')
optdepends=('xmltv: For an alternative source of programme listings')
provides=('tvheadend')
conflicts=('tvheadend' 'hts-tvheadend' 'hts-tvheadend-svn' 'tvheadend-git')
install=tvheadend.install
source=("${_gitname}::git+https://github.com/tvheadend/tvheadend.git#branch=master"
  'dvb-scan-tables::git+https://github.com/tvheadend/dtv-scan-tables.git#branch=tvheadend'
  'ignore-missing-libavresample.patch'
  'ua-VoliaCable'
  'tv_grab_file'
  'tvheadend.bin'
  'tvheadend.conf'
  'tvheadend.service')
md5sums=('SKIP'
         'SKIP'
         'ab3e00343f57b70b5e02a981e0a16055'
         '5c58449a9e8ab121d48e7139e55405ad'
         '6b3ee77305fe47e39fed09d04a32e383'
         '7c5ba5607c3850a96005c7a99722186f'
         '3a5a6b8e424225cf6bb99e80da54f43c'
         'c5d485a95e6afd17732deaddbba10aec')
backup=('etc/conf.d/tvheadend')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}"
  _dvbscan="${_gitname}/data/dvb-scan"
  rm -rf "${_dvbscan}"
  cp -a "dvb-scan-tables" "${_dvbscan}"
  rm -rf "${_dvbscan}/.git"
  touch "${_dvbscan}/.stamp"

  cd "${_gitname}"
  patch -p1 -i ../ignore-missing-libavresample.patch
}

build() {
  cd "${srcdir}/${_gitname}"

  # "Fix" build with GCC 8
  CFLAGS="${CFLAGS} \
  -Wno-error=stringop-truncation \
  -Wno-error=stringop-overflow \
  -Wno-error=discarded-qualifiers"

  ./configure --prefix=/usr --mandir=/usr/share/man/man1 --release \
  --python=python3 \
  --enable-avahi \
  --enable-zlib \
  --enable-hdhomerun_client \
  --enable-hdhomerun_static \
  --disable-ffmpeg_static --enable-libav \
  --disable-libx264_static --enable-libx264 \
  --disable-libx265_static --enable-libx265 \
  --disable-libvpx_static --enable-libvpx \
  --disable-libtheora_static --enable-libtheora \
  --disable-libvorbis_static --enable-libvorbis \
  --disable-libfdkaac_static --enable-libfdkaac \
  --disable-libopus_static --enable-libopus \
  --disable-nvenc \
  --enable-vaapi \
  --enable-inotify \
  --enable-epoll \
  --disable-pcre --enable-pcre2 \
  --enable-uriparser \
  --enable-dvben50221 \
  --enable-dbus_1 \
  --enable-tvhcsa

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir/" install
  install -D -m 755 "$srcdir/tv_grab_file" "$pkgdir/usr/bin/tv_grab_file"
  install -D -m 755 "$srcdir/tvheadend.bin" "$pkgdir/usr/bin/tvheadend.bin"
  install -D -m 644 "$srcdir/tvheadend.conf" "$pkgdir/etc/conf.d/tvheadend"
  install -D -m 644 "$srcdir/tvheadend.service" "$pkgdir/usr/lib/systemd/system/tvheadend.service"
  install -D -m 644 "$srcdir/ua-VoliaCable" "$pkgdir/usr/share/tvheadend/data/dvb-scan/dvb-c/ua-VoliaCable"
}

