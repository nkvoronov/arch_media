pkgname=vdr-softhddevice
_gitname='source-git'
_verpkg='0.7.0'
pkgver=0.7.0_g8cd57ae
_vdrapi=2.4.0
pkgrel=1
pkgdesc="software and GPU emulated HD output device plugin for VDR"
url="http://projects.vdr-developer.org/projects/plg-softhddevice"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('AGPL3')
depends=('ffmpeg3.4' 'mesa' "vdr-api=${_vdrapi}" 'xcb-util-wm' 'xorg-server')
optdepends=('nvidia: Required for VDPAU decoding',
            'vdr-xorg: Recommended way to start X.org server together with VDR')
makedepends=('git' 'glu')
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/ua0lnj/vdr-plugin-softhddevice.git#branch=vdpau+vaapi"
        'vdr-plugin-softhddevice-02_RU-locale.patch'
        "50-$_plugname.conf")
md5sums=('SKIP'
         '460ee389670858cd930698e95a7083c1'
         'f6ef6cb0bf17c5a28f858e5306776d27')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  # Disable OSS. Arch Linux doesn't ship OSS
  sed -i '/OSS /d' Makefile

  # Enable EOS_TRICKSPEED to fix rewind on 1080i channels
  # sed -i 's/#CONFIG += -DH264_EOS_TRICKSPEED/CONFIG += -DH264_EOS_TRICKSPEED/g' Makefile

  # Enable USE_MPEG_COMPLETE to fix partially missing video decoding of SD channels
  # sed -i 's/#CONFIG += -DUSE_MPEG_COMPLETE/CONFIG += -DUSE_MPEG_COMPLETE/g' Makefile

  # Enable this to increase AV_INFO logging
  #sed -i 's/3000/500/g' Makefile

  # Enable this to compile with debug logging
  #sed -i 's/# -DDEBUG/-DDEBUG/g' Makefile

  # Enable VA-API
  # sed -i 's/#VAAPI/VAAPI/g' Makefile
  # sed -i 's/#OPENGL/OPENGL/g' Makefile

  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  PKG_CONFIG_PATH="/usr/lib/ffmpeg3.4/pkgconfig" make
}

package() {
  cd "${srcdir}/${_gitname}"
  PKG_CONFIG_PATH="/usr/lib/ffmpeg3.4/pkgconfig" make DESTDIR="${pkgdir}" install
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
