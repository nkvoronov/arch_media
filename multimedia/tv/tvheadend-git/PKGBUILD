pkgname=tvheadend-git
_gitname='source-git'
pkgver=4.3.r1923.gaaca05cc1
pkgrel=1
pkgdesc="TV streaming server for Linux"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://tvheadend.org/"
license=('GPL3')

license=('GPL3')
depends=('avahi' 'ffmpeg' 'libdvbcsa' 'libfdk-aac' 'libhdhomerun' 'openssl' 'pcre2' 'uriparser')
makedepends=('git' 'python')
optdepends=('xmltv: For an alternative source of programme listings')
provides=('tvheadend')
conflicts=('tvheadend' 'hts-tvheadend' 'hts-tvheadend-svn' 'tvheadend-git')
install=tvheadend.install
source=("${_gitname}::git+https://github.com/tvheadend/tvheadend.git#branch=master"
  'dvb-scan-tables::git+https://github.com/tvheadend/dtv-scan-tables.git#branch=tvheadend'
  'ua-VoliaCable'
  'tv_grab_file'
  'tvheadend.bin'
  'tvheadend.conf'
  'tvheadend.service')
md5sums=('SKIP'
         'SKIP'
         '5c58449a9e8ab121d48e7139e55405ad'
         '25da1c0dd8bcaa3babf1bcd9a68153bd'
         '7c5ba5607c3850a96005c7a99722186f'
         '3a5a6b8e424225cf6bb99e80da54f43c'
         'ba6a2825cee0c331751b18aa07e2cc41')
backup=('etc/conf.d/tvheadend')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}"
  _dvbscan="${_gitname}/data/dvb-scan"
  rm -rf "${_dvbscan}"
  cp -a "dvb-scan-tables" "${_dvbscan}"
  rm -rf "${_dvbscan}/.git"
  touch "${_dvbscan}/.stamp"

  #cd "${srcdir}/${_gitname}"
  #for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC=/usr/bin/gcc-8
  export CXX=/usr/bin/g++-8

  ./configure \
  --prefix=/usr \
  --cc=$CC \
  --mandir=/usr/share/man/man1 \
  --nowerror \
  --python=python3 \
  --enable-avahi \
  --enable-bundle \
  --enable-pnquant \
  --enable-zlib \
  --disable-ffmpeg_static \
  --enable-libav \
  --disable-libx264_static \
  --enable-libx264 \
  --disable-libx265_static \
  --enable-libx265 \
  --disable-libvpx_static \
  --enable-libvpx \
  --disable-libtheora_static \
  --enable-libtheora \
  --disable-libvorbis_static \
  --enable-libvorbis \
  --disable-libfdkaac_static \
  --enable-libfdkaac \
  --disable-libopus_static \
  --enable-libopus \
  --disable-nvenc \
  --enable-vaapi \
  --enable-inotify \
  --enable-epoll \
  --disable-pcre \
  --enable-pcre2 \
  --disable-uriparser \
  --enable-dvben50221 \
  --disable-dvbscan \
  --enable-dbus_1 \
  --disable-hdhomerun_static \
  --enable-hdhomerun_client \
  --enable-tvhcs \
  --enable-dvbcsa \
  --enable-trace \

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir/" install
  install -D -m 755 "$srcdir/tv_grab_file" "$pkgdir/usr/bin/tv_grab_file"
  install -D -m 755 "$srcdir/tvheadend.bin" "$pkgdir/usr/bin/tvheadend.bin"
  install -D -m 644 "$srcdir/tvheadend.conf" "$pkgdir/etc/conf.d/tvheadend"
  install -D -m 644 "$srcdir/tvheadend.service" "$pkgdir/usr/lib/systemd/system/tvheadend.service"
  install -D -m 644 "$srcdir/ua-VoliaCable" "$pkgdir/usr/share/tvheadend/data/dvb-scan/dvb-c/ua-VoliaCable"
}

