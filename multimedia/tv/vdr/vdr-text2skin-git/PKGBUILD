# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-text2skin-git
_gitname='vdr-text2skin'
_verpkg='1.3.4'
pkgver=1.3.4_g8f7954d
_vdrapi=2.4.0
pkgrel=1
pkgdesc="VDR Text2Skin Plugin"
url="http://projects.vdr-developer.org/projects/plg-text2skin"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname='text2skin'

source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-text2skin.git#branch=master"
        'vdr-text2skin-01_RU-locale.patch')

md5sums=('SKIP'
         '31fad98051d7098d08693c410edc0746')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

package() {
    cd "${srcdir}/${_gitname}"

    mkdir -p "$pkgdir/usr/lib/vdr/plugins"
    make CFLAGS="$(pkg-config vdr --variable=cflags)" \
    	CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
        VDRDIR="/usr/include/vdr" \
        LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
        LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
        IMAGELIB=graphicsmagick

    mkdir -p "$pkgdir/etc/vdr/conf.avail"
    echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
