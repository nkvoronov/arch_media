pkgname=eventlircd
_gitname='source-git'
_gitrev=7faaf9d
pkgver=r54.7faaf9d
pkgrel=1
pkgdesc="A GNU/Linux daemon for simplifying udev based hotplugging of remote control devices."
arch=('i686' 'x86_64')
url="https://github.com/OpenELEC/$pkgname"
license=('GPL2')
depends=('libsystemd')
makedepends=('git')
conflicts=("$pkgname-svn")
replaces=("$pkgname-svn")
source=("${_gitname}::git://github.com/OpenELEC/$pkgname.git#commit=$_gitrev"
  "$pkgname.tmpfiles"
  "$pkgname.service"
  "eventlircd-disable-werror.patch")
md5sums=('SKIP'
  'febf25c154a7d36f01159e84f26c2d9a'
  'cbd30da2908532db31ddbd077a09c75a'
  '4315ee04ab5c73f2e13ec2c4295c49c0')

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  autoreconf -vfi
  ./configure \
  --prefix=/usr \
  --sysconfdir=/etc \
  --sbindir=/usr/bin \
  --disable-dependency-tracking \
  --disable-warnings \
  --with-lircd-socket=/run/lirc/lircd \
  --with-evmap-dir=/etc/eventlircd.d \
  --with-udev-dir=/usr/lib/udev

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  rm -f $pkgdir/etc/eventlircd.d/*
  rm -f $pkgdir/usr/lib/udev/rules.d/*
  rm -rf $pkgdir/usr/lib/udev/lircd_helper
  cp -PR ../../../evmap/* $pkgdir/etc/eventlircd.d
  cp -PR ../../../udev/* $pkgdir/usr/lib/udev/rules.d
}
