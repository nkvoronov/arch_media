# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-menuorg-git
_gitname='vdr-menuorg'
_verpkg='0.5.2'
pkgver=0.5.2_g2032189
_vdrapi=2.4.0
pkgrel=1
pkgdesc="VDR plug-in to reorganize the OSD main menu"
url="http://projects.vdr-developer.org/projects/plg-menuorg"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' 'libxml++2.6' 'glibmm' "vdr-api=${_vdrapi}")
makedepends=('git')
install="vdr-menuorg.install"
_plugname='menuorg'

source=("${_gitname}::git+https://github.com/vdr-projects/vdr-plugin-menuorg.git#branch=master"
    'vdr-menuorg-02_RU-locale.patch'
    'menuorg.xml.minimum')

md5sums=('SKIP'
         '99aa595a6593837639698eca92badf93'
         '94bdde78c39758c051f0ab9d618c12a3')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "menuorg.xml" "$pkgdir/usr/share/vdr/plugins/menuorg/menuorg.xml"
    install -Dm644 "menuorg.dtd" "$pkgdir/usr/share/vdr/plugins/menuorg/menuorg.dtd"

    install -Dm644 "$srcdir/menuorg.xml.minimum" "$pkgdir/usr/share/vdr/plugins/menuorg/menuorg.xml.minimum"

    mkdir -p "$pkgdir/etc/vdr/conf.avail"
    echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
