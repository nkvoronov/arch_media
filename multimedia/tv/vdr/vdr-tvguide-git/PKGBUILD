# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-tvguide-git
_gitname='vdr-tvguide'
_verpkg='1.2.2'
pkgver=1.2.2_g8287bb5
_vdrapi=2.2.0
pkgrel=1
pkgdesc="highly customizable 2D EPG viewer plugin."
url="http://projects.vdr-developer.org/projects/plg-tvguide"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' 'libpng' "vdr-api=$_vdrapi")
makedepends=('git')
_plugname='tvguide'

source=("${_gitname}::git+git://projects.vdr-developer.org/vdr-plugin-tvguide.git#branch=master"
        'vdr-tvguide-RU-locale.patch'
        "50-$_plugname.conf")
        
backup=("etc/vdr/conf.avail/50-$_plugname.conf")

md5sums=('SKIP'
         '65ca10fbbe69d9634c5a2ab21248e6ab'
         '5a624d5f3ef05f748da826dd16de3b84')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make IMAGELIB=graphicsmagick
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"

}
