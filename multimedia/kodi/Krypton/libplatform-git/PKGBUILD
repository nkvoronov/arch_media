# Maintainer: Cedric Girard <girard.cedric@gmail.com>

pkgname=libplatform-git
_gitname='libplatform'
pkgver=2.1.0.1
pkgrel=1
pkgdesc="Platform support library used by libCEC and binary add-ons for Kodi"
arch=('i686' 'x86_64')
url="https://github.com/Pulse-Eight/platform"
license=('GPL')
makedepends=('git')
provides=('libplatform' 'p8-platform')
conflicts=('libplatform' 'p8-platform')
source=("${_gitname}::git+https://github.com/Pulse-Eight/platform.git#tag=p8-platform-$pkgver")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir"/"${_gitname}"
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/p8\.platform\.//'
}

build() {
  cd "$srcdir"/"${_gitname}"
  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=1 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBDIR_NOARCH=/usr/lib
  make
}

package() {
  cd "$srcdir"/"${_gitname}"
  make DESTDIR="$pkgdir/" install
}
