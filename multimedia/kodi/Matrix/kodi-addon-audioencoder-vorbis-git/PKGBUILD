pkgname=kodi-addon-audioencoder-vorbis-git
_gitname='source-git'
_verpkg='3.0.1'
pkgver=3.0.1_g78aba10
pkgrel=1
pkgdesc="Vorbis Audio Encoder add-on for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/audioencoder.vorbis.git'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-audioencoder')
provides=('kodi-addon-audioencoder-vorbis-git')
replaces=('kodi-addon-audioencoder-vorbis-git')
conflicts=('kodi-addon-audioencoder-vorbis')
depends=('kodi')
makedepends=('cmake' 'kodi-dev')
source=("${_gitname}::git+https://github.com/xbmc/audioencoder.vorbis.git#branch=Matrix")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

build() {
  cd "${srcdir}/${_gitname}"
  cmake \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=Release \
  -DBUILD_SHARED_LIBS=1 \
  -DUSE_LTO=1
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir/" install
}
