# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-xvdr-git
_gitname='vdr-xvdr'
_verpkg='0.9.9'
pkgver=0.9.9_g2d5e3f6
_vdrapi=2.2.0
pkgrel=1
url="https://github.com/pipelka/vdr-plugin-xvdr"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
pkgdesc="VDR plugin to handle XBMC clients"
depends=('gcc-libs' "vdr-api=${_vdrapi}")
optdepends=('vdr-wirbelscan: Support channel searches with XBMC')
makedepends=('git')
_plugname='xvdr'

source=("${_gitname}::git+git://github.com/pipelka/vdr-plugin-xvdr.git#branch=master"
        "50-$_plugname.conf"
        'vdr-xvdr-makefile-gcc6.patch')

backup=('etc/vdr/plugins/xvdr/allowed_hosts.conf'
        'etc/vdr/plugins/xvdr/xvdr.conf'
        "etc/vdr/conf.avail/50-$_plugname.conf")

md5sums=('SKIP'
         '4e5a991284971e871808dba87e4b272b'
         'd66b0012ed76edd87125df4b66ff7383')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="${pkgdir}" install install-conf

    install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
