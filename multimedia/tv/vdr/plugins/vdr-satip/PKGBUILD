pkgname=vdr-satip
pkgver=2.4.0
_vdrapi=2.4.0
pkgrel=20
pkgdesc="Integrates SAT>IP network devices seamlessly into VDR"
url="http://www.saunalahti.fi/rahrenbe/vdr/satip/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('curl' 'pugixml' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("http://www.saunalahti.fi/~rahrenbe/vdr/satip/files/vdr-satip-$pkgver.tgz"
        "50-$_plugname.conf"
        'vdr-plugin-satip-02_RU-locale.patch')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
options=('!emptydirs')
md5sums=('2fec4e80db37e5d8f35aca825053ed20'
         '1b86395f44c2a7c63b97a7ef6f866ec0'
         '7686d8b71dfd1f9249710a29f5718ed8')

prepare() {
  cd "${srcdir}/$_plugname-${pkgver}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/$_plugname-${pkgver}"
  make GITTAG=''
}

package() {
  cd "${srcdir}/$_plugname-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
