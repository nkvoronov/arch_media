# Maintainer: swearchnick <swearchnick[at]gmail[dot]com>
_pkgname=linux_media
_gitname=media_build
pkgname="tbs-$_pkgname-git"
pkgver=r1149.639c351_4.20.0_arch1_1_ARCH
_extramodules=extramodules-ARCH
pkgrel=1
pkgdesc="TBS linux open source drivers"
arch=('x86_64')
url="https://github.com/tbsdtv/linux_media"
license=('GPL2')
depends=('tbs-firmware')
makedepends=('git' 'linux-headers' 'patchutils' 'perl-proc-processtable')
provides=("$_pkgname")
install='tbs-media_build-git.install'
conflicts=('tbs-dvb-drivers')
source=('add-to-backports.patch'
        'tbs-media_build-git.install')
md5sums=('288d7ee2daf331fee3812b270d79d9cb'
         '59f8329e80cbcb94235eda483983efc8')

prepare() {
  git clone https://github.com/tbsdtv/$_gitname.git
  git clone --depth=1 https://github.com/tbsdtv/$_pkgname.git -b latest "$srcdir/media"
  cd "${srcdir}/${_gitname}"
  cp -PR ../../backports/* backports/
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

pkgver() {
  cd "$srcdir/$_gitname"
  _gitver=$(printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)")
  _kernel=$(uname -r | sed -r 's/-/_/g')
  echo "$_gitver"_"$_kernel"
}

build() {
  cd "$srcdir/$_gitname"
  make dir DIR=../media
  make distclean
  make
}

package() {
  cd "$srcdir/$_gitname"
  mkdir -p "$pkgdir"/usr/lib/modules/"${_extramodules}"/tbs
  find "$srcdir/$_gitname" -name '*.ko' -exec cp "{}" "$pkgdir"/usr/lib/modules/"${_extramodules}"/tbs \;
  msg "Compressing modules, this will take awhile..."
  find "$pkgdir" -name '*.ko' -print0 | xargs -0 -P"$(nproc)" -n10 gzip -9
  chmod 0644 "$pkgdir"/usr/lib/modules/"${_extramodules}"/tbs/*.ko.gz
}
