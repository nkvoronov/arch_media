# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-skinflat-git
_gitname='vdr-skinflat'
_verpkg='0.1.1'
pkgver=0.1.1_g1040bb7
_vdrapi=2.2.0
pkgrel=1
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflat"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
_plugname='skinflat'

source=("${_gitname}::git+git://projects.vdr-developer.org/skin-flat.git#branch=master"
        'vdr-skinflat-fixsource.patch'
        'vdr-skinflat-RU-locale.patch'
        "50-$_plugname.conf")
        
backup=("etc/vdr/conf.avail/50-$_plugname.conf")

md5sums=('SKIP'
         '3f4e2685b0c4a76e16b6d4a1b51270b8'
         '79c0ab68af5d23e80bc7664f874b61a7'
         '6dc69d30a36385dc4f15ce0b83d6c0c7')
         
pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
