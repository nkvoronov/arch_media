pkgname=vdr-ddci2
pkgver=1.0.5_g05dd988
_verpkg=1.0.5
_gitname='source-git'
_vdrapi=2.4.0
pkgrel=20
pkgdesc="Support for stand alone CI by Digital Devices for VDR 2.1.6 and higher."
url="https://github.com/jasmin-j/vdr-plugin-ddci2"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/jasmin-j/vdr-plugin-ddci2.git#branch=master"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         '5dc8a287efe1419aaf1162cb6a6f28f3')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
