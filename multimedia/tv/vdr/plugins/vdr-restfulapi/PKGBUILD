pkgname=vdr-restfulapi
_gitname='source-git'
_verpkg='0.2.6.5'
epoch=1
pkgver=0.2.6.5_g8712e44
pkgrel=1
_vdrapi=2.4.0
pkgdesc="Restful-API Control for VDR"
url="https://github.com/yavdr/vdr-plugin-restfulapi"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('cxxtools' 'graphicsmagick' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/yavdr/vdr-plugin-restfulapi.git#branch=master"
        "50-$_plugname.conf"
        'vdr-plugin-restfulapi-02_RU-locale.patch')
backup=("etc/vdr/conf.avail/50-$_plugname.conf"
        'etc/vdr/plugins/restfulapi/API.html'
        'etc/vdr/plugins/restfulapi/osd.css'
        'etc/vdr/plugins/restfulapi/osd.js')
md5sums=('SKIP'
         '7b482249828a58f1f6ce2cd4a577f773'
         '33ee2c42e4f51be00e288cc3fd2751c3')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -m 644 web/osd.js     "$pkgdir/etc/vdr/plugins/restfulapi/"
  install -m 644 web/osd.css    "$pkgdir/etc/vdr/plugins/restfulapi/"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
