pkgname=vdr-epgsearch
pkgver=2.4.0_g84b59b8
_gitname='source-git'
_verpkg='2.4.0'
_vdrapi=2.4.0
epoch=1
pkgrel=20
pkgdesc="Searchtimer and replacement of the VDR program menu"
url="http://winni.vdr-developer.org/epgsearch"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
optdepends=('msmtp: To send notification mails (Simpler replacement for sendmail)'
            'ssmtp: To send notification mails (Another simpler replacement for sendmail)')
makedepends=('git')
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/vdr-projects/vdr-plugin-epgsearch.git#branch=master"
        "50-$_plugname.conf"
        'vdr-plugin-epgsearch-01_debian-defaults.patch'
        'vdr-plugin-epgsearch-02_RU-locale.patch')
backup=("etc/vdr/conf.avail/50-conflictcheckonly.conf"
        "etc/vdr/conf.avail/50-$_plugname.conf"
        "etc/vdr/conf.avail/50-${_plugname}only.conf"
        "etc/vdr/conf.avail/50-quick$_plugname.conf"
        'etc/vdr/plugins/epgsearch/epgsearchcats.conf-epgdata'
        'etc/vdr/plugins/epgsearch/epgsearchcats.conf-tvm2vdr-hoerzu'
        'etc/vdr/plugins/epgsearch/epgsearchcats.conf-tvm2vdr-tvmovie'
        'etc/vdr/plugins/epgsearch/epgsearchconflmail.templ'
        'etc/vdr/plugins/epgsearch/epgsearchmenu.conf'
        'etc/vdr/plugins/epgsearch/epgsearchupdmail-html.templ'
        'etc/vdr/plugins/epgsearch/epgsearchupdmail.templ')
options=('!emptydirs')
md5sums=('SKIP'
         '9a936790ce844d011d8bbb3bf13ec336'
         'e7add7d4e1958adbed7568be23251ee2'
         'b02a3719e1b13039de339d98a236c840')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

#prepare() {
#  cd "${srcdir}/${_gitname}"
#  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
#}

build() {
  cd "${srcdir}/${_gitname}"
  make REGEXLIB=''
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
  echo "[${_plugname}only]" > "$pkgdir/etc/vdr/conf.avail/50-${_plugname}only.conf"
  echo "[conflictcheckonly]" > "$pkgdir/etc/vdr/conf.avail/50-conflictcheckonly.conf"
  echo "[quick${_plugname}]" > "$pkgdir/etc/vdr/conf.avail/50-quick${_plugname}.conf"
}
