pkgname=vdr-skinflat
_gitname='source-git'
_verpkg='0.1.1'
pkgver=0.1.1_g1040bb7
_vdrapi=2.4.0
pkgrel=20
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflat"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('git' 'graphicsmagick' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-skinflat.git#branch=master"
        'vdr-plugin-skinflat-02_fixsource.patch'
        'vdr-plugin-skinflat-03_RU-locale.patch'
        "50-$_plugname.conf")
md5sums=('SKIP'
         '3f4e2685b0c4a76e16b6d4a1b51270b8'
         '79c0ab68af5d23e80bc7664f874b61a7'
         'e6ba017eb9caf71f6ce4dc622a1a2bc8')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  sed -i 's/Magick++/GraphicsMagick++/g' Makefile
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
