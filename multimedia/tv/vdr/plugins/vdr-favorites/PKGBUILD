pkgname=vdr-favorites
pkgver=0.0.2_g8d3f3a5
_gitname='source-git'
_verpkg='0.0.2'
_vdrapi=2.4.0
pkgrel=20
pkgdesc="Implements a favorite channels menu"
url="http://www.vdr-portal.de/board1-news/board2-vdr-news/p1039565-announce-vdr-favorites-0-0-2/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/nkvoronov/vdr-plugin-favorites.git#branch=master"
        'vdr-plugin-favorites-01_makefile.patch'
        'vdr-plugin-favorites-02_vdr-2.3.1-porting.patch'
        'vdr-plugin-favorites-03_RU-locale.patch')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         'b1327695f632d33c768476d72aa3dc98'
         'c57b5035500536ff9f867a729936563a'
         '6d7a717c7499bccf74e594e88faeb3db')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
