pkgname=kodi-addon-audioencoder-flac
_gitname='source-git'
_verpkg='19.0.0'
pkgver=19.0.0_g6fbbd42
pkgrel=1
pkgdesc="FLAC Audio Encoder add-on for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/audioencoder.flac.git'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-audioencoder')
provides=('kodi-audioencoder-flac')
replaces=('kodi-audioencoder-flac')
conflicts=('kodi-audioencoder-flac')
depends=('kodi')
makedepends=('cmake' 'kodi-dev')
source=("${_gitname}::git+https://github.com/xbmc/audioencoder.flac.git#tag=${_verpkg}-Matrix")

md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

build() {
  cd "${srcdir}/${_gitname}"
  cmake \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=Release \
  -DBUILD_SHARED_LIBS=1 \
  -DUSE_LTO=1
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir/" install
}
