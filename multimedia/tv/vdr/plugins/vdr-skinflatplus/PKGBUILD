pkgname=vdr-skinflatplus
_gitname='source-git'
_verpkg='0.6.0'
pkgver=0.6.0_g9f49ddbd
_vdrapi=2.4.0
pkgrel=20
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflatplus"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('fontconfig' 'graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-skinflatplus.git#branch=master"
        'vdr-plugin-skinflatplus-01_makefile.patch'
        'vdr-plugin-skinflatplus-02_fixsource.patch'
        'vdr-plugin-skinflatplus-03_RU-locale.patch'
        "50-$_plugname.conf")
md5sums=('SKIP'
         '1f554e17285948f904382d62277f8f45'
         'a57bb0a835c7956fb7ab68618bf28860'
         '0eec60cfddbf2f387ba8b87f28b51cff'
         '878594e0f5af2ab308e6ab86a9bf0136')
backup=('etc/epgd/eventsview-flatplus.sql'
        "etc/vdr/conf.avail/50-$_plugname.conf"
        'etc/vdr/plugins/skinflatplus/configs/'{MV_default,default,fnu_default})

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  sed -i 's/Magick++/GraphicsMagick++/g' Makefile
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 contrib/eventsview-flatplus.sql "$pkgdir/etc/epgd/eventsview-flatplus.sql"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
