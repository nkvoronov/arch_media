# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-vnsiserver-git
_gitname='vdr-vnsiserver'
_verpkg='1.5.2'
pkgver=1.5.2_g0c84792
_vdrapi=2.4.0
pkgrel=1
url="https://github.com/FernetMenta/vdr-plugin-vnsiserver"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
pkgdesc="VDR plugin to handle XBMC clients"
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname='vnsiserver'

source=("${_gitname}::git+git://github.com/FernetMenta/vdr-plugin-vnsiserver#branch=master"
        "plugin.${_plugname}.conf")

md5sums=('SKIP'
         '2b9ea1c1c0709bad1ddfc4bdb65628d0')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf"
        'etc/vdr/plugins/vnsiserver/allowed_hosts.conf')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="${pkgdir}" install

    mkdir -p "$pkgdir/etc/vdr/plugins"
    cp -r vnsiserver "$pkgdir/etc/vdr/plugins"
    mkdir -p "$pkgdir/etc/vdr/conf.avail"
    install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
