# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
# Contributor: Ole Ernst <olebowle[at]gmx[dot]com
pkgbase=vdr-streamdev
_gitname='vdr-streamdev'
pkgname=(vdr-streamdev-{client,server}-git)
_verpkg='0.6.1'
pkgver=0.6.1_ge2a9b97
_vdrapi=2.4.0
pkgrel=1
pkgdesc="implementation of the VTP (Video Transfer Protocol)"
url="http://projects.vdr-developer.org/projects/show/plg-streamdev"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname='streamdev'

source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-streamdev.git#branch=master"
        'vdr-streamdev-01_4k_hevc_support.patch'
        'vdr-streamdev-02_RU-locale.patch'
        "plugin.${_plugname}-server.conf")

md5sums=('SKIP'
         '541b8cdbc0dfd317d75fec30432ec846'
         '99e9be776b59903156fe85d02da6eb70'
         'c22ba9bffb11c96ab792758c51b2777a')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"

    #for f in "$srcdir"/*.diff; do patch -p1 < "$f"; done
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done

    sed -i 's/m_Buffer\[4096\]/m_Buffer\[8192\]/' client/filter.c
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package_vdr-streamdev-client-git() {
    pkgdesc+=': client-side'
    replaces=('vdr-streamdev')
    conflicts=('vdr-streamdev')

    backup=("etc/vdr/conf.avail/plugin.${_plugname}-client.conf")

    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install-client

    mkdir -p "$pkgdir/etc/vdr/conf.avail"
    echo "[$_plugname-client]" > "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}-client.conf"
}

package_vdr-streamdev-server-git() {
    pkgdesc+=': server-side'
    replaces=('vdr-streamdev')
    conflicts=('vdr-streamdev')

    backup=("etc/vdr/conf.avail/plugin.${_plugname}-server.conf"
            'etc/vdr/plugins/streamdev-server/streamdevhosts.conf')

    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install-server

    install -Dm644 streamdev-server/streamdevhosts.conf "$pkgdir/etc/vdr/plugins/streamdev-server/streamdevhosts.conf"
    install -Dm755 streamdev-server/externremux.sh "$pkgdir/etc/vdr/plugins/streamdev-server/externremux.sh"

    mkdir -p "$pkgdir/etc/vdr/conf.avail"
    install -Dm644 "$srcdir/plugin.${_plugname}-server.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}-server.conf"
}
