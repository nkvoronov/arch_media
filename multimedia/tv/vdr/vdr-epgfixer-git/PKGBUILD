# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-epgfixer-git
_gitname='vdr-epgfixer'
_verpkg='0.3.1'
pkgver=0.3.1_g354f28b
_vdrapi=2.4.0
pkgrel=1
pkgdesc="VDR plugin for doing extra fixing of EPG data"
url="http://projects.vdr-developer.org/projects/plg-epgfixer"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' 'pcre' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname='epgfixer'

source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-epgfixer.git#branch=master"
        'vdr-epgfixer-RU-locale.patch')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf"
        'etc/vdr/plugins/epgfixer/blacklist.conf'
        'etc/vdr/plugins/epgfixer/charset.conf'
        'etc/vdr/plugins/epgfixer/epgclone.conf'
        'etc/vdr/plugins/epgfixer/regexp.conf')

md5sums=('SKIP'
         '747f8537ba24aac066ebc3b625bf3478')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    mkdir -p "$pkgdir/etc/vdr/conf.avail"
    echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
