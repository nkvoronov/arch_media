# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-epgsearch-git
_gitname='vdr-epgsearch'
_verpkg='1.0.1'
pkgver=1.0.1_g84b59b8
_vdrapi=2.4.0
pkgrel=1
pkgdesc="Searchtimer and replacement of the VDR program menu"
url="http://winni.vdr-developer.org/epgsearch"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
optdepends=('msmtp: To send notification mails (Simpler replacement for sendmail)'
            'ssmtp: To send notification mails (Another simpler replacement for sendmail)')
makedepends=('git')
_plugname='epgsearch'

source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-epgsearch.git#branch=master"
        'epg2taste.sh'
        'epgsearchcats.conf'
        'epgsearchcmds.conf'
        'epgsearchmenu.conf'
        'rememberevent.sh'
        'vdr-epgsearch-RU-locale.patch'
        "plugin.${_plugname}.conf")

md5sums=('SKIP'
         '3407c215858bcc105a5f16c60f751629'
         '7ff1789a8f29033494191d81429e6192'
         '8ebd06819eb0dc7e8d0ce94612fc7f6c'
         '49dd23e2bc610978500259adbdf581eb'
         'fd9bc294515e6fffa083a7b6f7318093'
         'b02a3719e1b13039de339d98a236c840'
         'ad13a1fc07c67bb1b4fc5bcb8ec4d404')

backup=("etc/vdr/conf.avail/plugin.conflictcheckonly.conf"
        "etc/vdr/conf.avail/plugin.${_plugname}.conf"
        "etc/vdr/conf.avail/plugin.${_plugname}only.conf"
        "etc/vdr/conf.avail/plugin.quick${_plugname}.conf"
        'etc/vdr/plugins/epgsearch/epgsearchcats.conf-epgdata'
        'etc/vdr/plugins/epgsearch/epgsearchcats.conf-tvm2vdr-hoerzu'
        'etc/vdr/plugins/epgsearch/epgsearchcats.conf-tvm2vdr-tvmovie'
        'etc/vdr/plugins/epgsearch/epgsearchconflmail.templ'
        'etc/vdr/plugins/epgsearch/epgsearchmenu.conf'
        'etc/vdr/plugins/epgsearch/epgsearchupdmail-html.templ'
        'etc/vdr/plugins/epgsearch/epgsearchupdmail.templ')

options=('!emptydirs')


pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    #for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
    sed -i 's/^Menu/# Menu/g' conf/epgsearchmenu.conf
}

build() {
    cd "${srcdir}/${_gitname}"
    make REGEXLIB='' #Empty REGEXLIB to force regex.h from glibc
}


package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
    echo "[${_plugname}only]" > "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}only.conf"
    echo "[conflictcheckonly]" > "$pkgdir/etc/vdr/conf.avail/plugin.conflictcheckonly.conf"
    echo "[quick${_plugname}]" > "$pkgdir/etc/vdr/conf.avail/plugin.quick${_plugname}.conf"

    mkdir -p "$pkgdir/etc/vdr/plugins/epgsearch"
    install -Dm644 "$srcdir/epgsearchcats.conf" "$pkgdir/etc/vdr/plugins/epgsearch/epgsearchcats.conf"
    install -Dm644 "$srcdir/epgsearchcmds.conf" "$pkgdir/etc/vdr/plugins/epgsearch/epgsearchcmds.conf"
    install -Dm644 "$srcdir/epgsearchmenu.conf" "$pkgdir/etc/vdr/plugins/epgsearch/epgsearchmenu.conf"

    mkdir -p "$pkgdir/usr/share/vdr/plugins/epgsearch"
    install -Dm755 "$srcdir/epg2taste.sh" "$pkgdir/usr/share/vdr/plugins/epgsearch/epg2taste.sh"
    install -Dm755 "$srcdir/rememberevent.sh" "$pkgdir/usr/share/vdr/plugins/epgsearch/rememberevent.sh"
}
