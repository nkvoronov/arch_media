pkgname=kodi-addon-vfs-sftp-git
_gitname='source-git'
_verpkg='2.0.0'
pkgver=2.0.0_g3a40cda
pkgrel=1
pkgdesc='SFTP Addon for Kodi'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/xbmc/vfs.sftp"
license=('GPL')
groups=('kodi-addons')
makedepends=('cmake' 'kodi-platform' 'git' 'kodi-devel-dev')
depends=('kodi-devel' 'p8-platform')
source=("${_gitname}::git+https://github.com/xbmc/vfs.sftp.git#branch=Matrix")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

build() {
  cd "${srcdir}/${_gitname}"
  mkdir -p build && pushd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
  popd
}

package() {
  cd "${srcdir}/${_gitname}/build"
  make DESTDIR="$pkgdir" install
}
