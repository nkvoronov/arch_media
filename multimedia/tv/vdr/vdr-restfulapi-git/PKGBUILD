# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Alexander Grothe <seahawk1986[at]hotmail[dot]com>
pkgname=vdr-restfulapi-git
_gitname='vdr-restfulapi'
_verpkg='0.2.6.5'
pkgver=0.2.6.5_g96a03b3
pkgrel=1
_vdrapi=2.2.0
pkgdesc="Restful-API Control for VDR"
url="https://github.com/yavdr/vdr-plugin-restfulapi"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('cxxtools' 'graphicsmagick' "vdr-api=${_vdrapi}")
_plugname='restfulapi'

source=("${_gitname}::git+https://github.com/yavdr/vdr-plugin-restfulapi.git#branch=master"
	'vdr-restfulapi-RU-locale.patch'
        "50-$_plugname.conf")

backup=("etc/vdr/conf.avail/50-$_plugname.conf"
        'etc/vdr/plugins/restfulapi/API.html'
        'usr/share/vdr/plugins/restfulapi/osd.css'
        'usr/share/vdr/plugins/restfulapi/osd.js')

md5sums=('SKIP'
         '33ee2c42e4f51be00e288cc3fd2751c3'
         'c65fdc8d51d9d9d9dfd6c84dd07eff6e')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    mkdir -p "$pkgdir/usr/share/vdr/plugins/restfulapi"
    install -m 644 web/osd.js     "$pkgdir/usr/share/vdr/plugins/restfulapi/"
    install -m 644 web/osd.css    "$pkgdir/usr/share/vdr/plugins/restfulapi/"

    install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
