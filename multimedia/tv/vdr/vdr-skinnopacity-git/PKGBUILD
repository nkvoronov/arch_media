# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-skinnopacity-git
_gitname='vdr-skinnopacity'
_verpkg='1.1.3'
pkgver=1.1.3_gf593ad5
_vdrapi=2.2.0
pkgrel=1
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflatplus"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname='skinnopacity'

source=("${_gitname}::git+git://projects.vdr-developer.org/skin-nopacity.git#branch=master"
        'vdr-skinnopacity-RU-locale.patch'
        "50-$_plugname.conf")
        
backup=("etc/vdr/conf.avail/50-$_plugname.conf"
	'etc/vdr/plugins/skinnopacity/themeconfigs/theme-darkredNG.conf'
	'etc/vdr/plugins/skinnopacity/themeconfigs/theme-default.conf'
	'etc/vdr/plugins/skinnopacity/themeconfigs/theme-iceblue.conf'
	'etc/vdr/plugins/skinnopacity/themeconfigs/theme-light.conf')

md5sums=('SKIP'
         '7c1ffdea059d6e26a3157c48a78e5539'
         'd9cbf3c31f08be59f8fe840cc2c81cb3')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
