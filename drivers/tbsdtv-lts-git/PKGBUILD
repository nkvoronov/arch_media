pkgname="tbsdtv-lts-git"
_gitname='source-git'
_gitname2='source-git2'
pkgver=r1285.50375e4_5.4.32_1_lts
pkgrel=1
pkgdesc="TBS linux open source drivers"
arch=('x86_64')
url="https://github.com/tbsdtv/linux_media"
license=('GPL2')
depends=('tbsdtv-firmware')
makedepends=('git' 'linux-lts-headers' 'patchutils' 'perl-proc-processtable')
provides=("tbsdtv")
install='tbsdtv-git.install'
conflicts=('tbs-linux_media-git')
source=("${_gitname}::git+https://github.com/tbsdtv/media_build.git"
        "${_gitname2}::git+https://github.com/tbsdtv/linux_media.git#branch=latest"
        'add-to-backports.patch'
        'tbsdtv-git.install')
md5sums=('SKIP'
         'SKIP'
         '288d7ee2daf331fee3812b270d79d9cb'
         '59f8329e80cbcb94235eda483983efc8')

prepare() {
  cd "${srcdir}/${_gitname}"
  cp -PR ../../backports/* backports/
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  _gitver=$(printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)")
  _kernel=$(uname -r | sed -r 's/-/_/g')
  echo "$_gitver"_"$_kernel"
}

build() {
  cd "${srcdir}/${_gitname}"
  make dir DIR=../${_gitname2}
  make distclean
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  _kernel=$(uname -r)
  mkdir -p "$pkgdir"/usr/lib/modules/"${_kernel}"/extramodules/tbs
  find "$srcdir/$_gitname" -name '*.ko' -exec cp "{}" "$pkgdir"/usr/lib/modules/"${_kernel}"/extramodules/tbs \;
  msg "Compressing modules, this will take awhile..."
  find "$pkgdir" -name '*.ko' -print0 | xargs -0 -P"$(nproc)" -n10 gzip -9
  chmod 0644 "$pkgdir"/usr/lib/modules/"${_kernel}"/extramodules/tbs/*.ko.gz
}
