pkgname=vdr-femon
pkgver=2.4.0
_vdrapi=2.4.0
pkgrel=20
pkgdesc="DVB Frontend Status Monitor plugin"
url="http://www.saunalahti.fi/rahrenbe/vdr/femon/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("http://www.saunalahti.fi/~rahrenbe/vdr/femon/files/$pkgname-$pkgver.tgz"
        'vdr-plugin-femon-02_RU-locale.patch')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('1771c79e5345772199f56c42a5ee73cd'
         'd6ea0fbf9eb66353f18703aa7e83a284')

prepare() {
  cd "${srcdir}/$_plugname-${pkgver}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/$_plugname-${pkgver}"
  make GITTAG=''
}

package() {
  cd "${srcdir}/$_plugname-${pkgver}"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
