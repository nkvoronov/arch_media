# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-satip
pkgver=2.4.0
_vdrapi=2.4.0
pkgrel=1
pkgdesc="Integrates SAT>IP network devices seamlessly into VDR"
url="http://www.saunalahti.fi/rahrenbe/vdr/satip/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('curl' 'pugixml' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}

source=("http://www.saunalahti.fi/~rahrenbe/vdr/satip/files/$pkgname-$pkgver.tgz"
        "plugin.${_plugname}.conf"
        'vdr-satip-02_RU-locale.patch')

md5sums=('2fec4e80db37e5d8f35aca825053ed20'
         '34190df0b6b8269490ff977f01ff4192'
         '7686d8b71dfd1f9249710a29f5718ed8')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

options=('!emptydirs')

prepare() {
    cd "${srcdir}/${_plugname}-${pkgver}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_plugname}-${pkgver}"
    make GITTAG=''
}


package() {
    cd "${srcdir}/${_plugname}-${pkgver}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
