pkgname=kodi-addon-pvr-hts-git
_gitname='source-git'
_verpkg='4.4.7'
pkgver=4.4.7_gb6a5202
pkgrel=1
pkgdesc='Tvheadend HTSP PVR client addon for Kodi'
arch=('armv7h' 'i686' 'x86_64')
url="https://github.com/kodi-pvr/pvr.hts"
license=('GPL3')
groups=('kodi')
makedepends=('cmake' 'git' 'kodi-platform-git')
depends=('kodi-git')
provides=('kodi-addon-pvr-hts')
conflicts=('kodi-addon-pvr-hts' 'kodi-pvr-addons')
source=("${_gitname}::git+https://github.com/kodi-pvr/pvr.hts.git")
md5sums=('SKIP')
API=99

case "$API" in
  15)  depends+=('libplatform-legacy')
       source[0]="${_gitname}::git+https://github.com/kodi-pvr/pvr.hts.git#branch=Isengard" ;;
  16)  depends+=('libplatform-legacy')
       source[0]="${_gitname}::git+https://github.com/kodi-pvr/pvr.hts.git#branch=Jarvis" ;;
  17)  depends+=('p8-platform')
       makedepends+=('kodi-dev-git')
       source[0]="${_gitname}::git+https://github.com/kodi-pvr/pvr.hts.git#branch=Krypton" ;;
  99)  depends+=('p8-platform')
       makedepends+=('kodi-dev-git') ;;
  *)   error "Unknown API version. Follow instructions in PKGBUILD." && false
esac

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

build() {
  cd "${srcdir}/${_gitname}"

  rm -rf build
  mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib/kodi \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_MODULE_PATH=/usr/lib/kodi \
    ..
  make
}

package() {
  cd "${srcdir}/${_gitname}/build"
  make DESTDIR="$pkgdir" install
}
