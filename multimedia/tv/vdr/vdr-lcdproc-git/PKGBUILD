# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-lcdproc-git
_gitname='vdr-lcdproc'
_verpkg='0.0.10_jw9'
pkgver=0.0.10_jw9_g0c54897
_vdrapi=2.4.0
pkgrel=1
pkgdesc="Output to LCD modules that are supported by LCDproc"
url="http://projects.vdr-developer.org/projects/plg-lcdproc"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
optdepends=('lcdproc: to use local displays')
_plugname='lcdproc'

source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-lcdproc.git#branch=master"
        'vdr-lcdproc-02_dynamite.patch'
        'vdr-lcdproc-03_vdr-2.3.2-v2.patch'
        'vdr-lcdproc-04_RU-locale.patch'
        "plugin.${_plugname}.conf"
        '92-lcdproc.rules')

md5sums=('SKIP'
         '113046cc762f72d8690e465e901309c7'
         '047746f6f729704ec93da66736480c6e'
         'e47d19483cef51b124e4e4ac588b0dc1'
         '7bddabcfc0fdd7b703d07d692a1f1859'
         'df36e7f2dc7f15570989b3baa4f50c78')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/92-lcdproc.rules" "$pkgdir/usr/lib/udev/rules.d/92-lcdproc.rules"
    install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
