pkgname=lcdproc
pkgver=0.5.9
pkgrel=10
pkgdesc="Utility to drive one or more LCD (and LCD-like) devices"
arch=('x86_64')
url="http://lcdproc.omnipotent.net/"
license=('GPL')
depends=('libftdi-compat')
optdepends=(
    'perl: needed for some lcdproc tools'
)
backup=(
    'etc/LCDd.conf'
    'etc/lcdexec.conf'
    'etc/lcdproc.conf'
    'etc/lcdvc.conf'
)
source=(
    "https://github.com/lcdproc/lcdproc/releases/download/v$pkgver/lcdproc-$pkgver.tar.gz"
    'cp1251.fnt'
    'iso8859_15.fnt'
    'lcd_numbers.py'
    'lcd_output.py'
    'lcd_text.py'
    'lcdd.service'
    'lcdproc.service'
    'lcdproc-0.5.9-fix-fno-common-build.patch'
    'lcdproc-0.5.6-imon_cp1251_fonts.patch'
)
md5sums=('c881a0c2c04491e42a06fcb90acd9140'
         'c2e77b239ef4d1b7c7fcdb69e4afce36'
         'd2d65b2e6f97eb3be10d902e28d2bf39'
         '7daec0195c7c9143331fc5381a286fbc'
         'ab7f5366557d4395b8d22c17e741a57a'
         '5290b8bf8c7afd5d0c9c655680086f5f'
         '36c2712682d412e37e5e6b02985a2780'
         '151697013886db9617b6a57907879dc3'
         'dc9b469e659c4746e3595e17a596bd2c'
         '7869e97c407431c25bb3b3a489d59428')

prepare() {
  cd "$pkgname-$pkgver"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
  --prefix=/usr \
  --sbindir=/usr/bin \
  --sysconfdir=/etc \
  --enable-libusb \
  --enable-lcdproc-menus \
  --enable-extra-charmaps \
  --enable-stat-smbfs \
  --enable-drivers=all
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  sed -e "s/server\/drivers\//\/usr\/lib\/lcdproc\//g" -i "$pkgdir/etc/LCDd.conf"
  install -Dm644 "$srcdir/lcdd.service" "$pkgdir/usr/lib/systemd/system/lcdd.service"
  install -Dm644 "$srcdir/lcdd.service" "$pkgdir/usr/lib/systemd/system/lcdproc.service"
  install -Dm644 "$srcdir/cp1251.fnt" "$pkgdir/usr/share/lcdproc/fonts/cp1251.fnt"
  install -Dm644 "$srcdir/iso8859_15.fnt" "$pkgdir/usr/share/lcdproc/fonts/iso8859_15.fnt"
  install -Dm755 "$srcdir/lcd_numbers.py" "$pkgdir/usr/share/lcdproc/examples/lcd_numbers.py"
  install -Dm755 "$srcdir/lcd_output.py" "$pkgdir/usr/share/lcdproc/examples/lcd_output.py"
  install -Dm755 "$srcdir/lcd_text.py" "$pkgdir/usr/share/lcdproc/examples/lcd_text.py"
}

