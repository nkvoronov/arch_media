pkgname=tvheadend-git
_gitname='source-git'
pkgver=4.3.r2033.g4741b3c19
pkgrel=1
pkgdesc="TV streaming server for Linux"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://tvheadend.org/"
license=('GPL3')
depends=(
  'gcc11'
  'avahi'
  'ffmpeg4.4'
  'libavresample'
  'libdvbcsa'
  'libfdk-aac'
  'libhdhomerun'
  'libogg'
  'libtheora'
  'libvorbis'
  'libvpx'
  'openssl'
  'opus'
  'pcre2'
  'pngquant'
  'uriparser'
  'x264'
  'x265'
)
makedepends=(
  'git'
  'python'
)
optdepends=(
  'xmltv: For an alternative source of programme listings'
)
options=('!strip' 'emptydirs')
provides=('tvheadend')
conflicts=('tvheadend')
install=tvheadend.install
source=("${_gitname}::git+https://github.com/tvheadend/tvheadend.git#branch=master"
  'dvb-scan-tables::git+https://github.com/tvheadend/dtv-scan-tables.git#branch=tvheadend'
  'ua-VoliaCable'
  'tv_grab_file'
  'tvheadend.bin'
  'tvheadend.conf'
  'tvheadend.service'
  '0001-use-ffmpeg4.4.patch')

md5sums=('SKIP'
         'SKIP'
         '5c58449a9e8ab121d48e7139e55405ad'
         '25da1c0dd8bcaa3babf1bcd9a68153bd'
         '7c5ba5607c3850a96005c7a99722186f'
         'd54ba590fd00e60454fd547db6b4c270'
         'ba6a2825cee0c331751b18aa07e2cc41'
         '6f750efebb81054adb02dca76254cbcd')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}"
  _dvbscan="${_gitname}/data/dvb-scan"
  rm -rf "${_dvbscan}"
  cp -a "dvb-scan-tables" "${_dvbscan}"
  rm -rf "${_dvbscan}/.git"
  touch "${_dvbscan}/.stamp"

  cd "${srcdir}/${_gitname}"
  patch -Np1 -i ../0001-use-ffmpeg4.4.patch
}

build() {
  cd "${srcdir}/${_gitname}"

  # Work-around for GCC 10
  export CFLAGS="${CFLAGS} -fcommon"

  ./configure \
    --prefix=/usr \
    --cc=gcc-11 \
    --datadir=/var/lib \
    --mandir=/usr/share/man/man1 \
    --python=python3 \
    --enable-avahi \
    --enable-zlib \
    --enable-pngquant \
    --enable-libav \
    --disable-ffmpeg_static --enable-ffmpeg \
    --disable-libx264_static --enable-libx264 \
    --disable-libx265_static --enable-libx265 \
    --disable-libvpx_static --enable-libvpx \
    --disable-libogg_static --enable-libogg \
    --disable-libtheora_static --enable-libtheora \
    --disable-libvorbis_static --enable-libvorbis \
    --disable-libfdkaac_static --enable-libfdkaac \
    --disable-libopus_static --enable-libopus \
    --disable-hdhomerun_static --enable-hdhomerun_client

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir/" install
  install -D -m 755 "$srcdir/tv_grab_file" "$pkgdir/usr/bin/tv_grab_file"
  install -D -m 755 "$srcdir/tvheadend.bin" "$pkgdir/usr/bin/tvheadend.bin"
  install -D -m 644 "$srcdir/tvheadend.conf" "$pkgdir/etc/conf.d/tvheadend"
  install -D -m 644 "$srcdir/tvheadend.service" "$pkgdir/usr/lib/systemd/system/tvheadend.service"
  install -D -m 644 "$srcdir/ua-VoliaCable" "$pkgdir/usr/share/tvheadend/data/dvb-scan/dvb-c/ua-VoliaCable"
}

