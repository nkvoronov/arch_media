# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname='vdr-skindesigner-git'
_gitname='vdr-skindesigner'
_verpkg='1.2.3'
pkgver=1.2.3_gc8d54db
_vdrapi=2.2.0
pkgrel=1
pkgdesc="Highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('git' 'librsvg' 'ttf-opensans' 'unzip' "vdr-api=${_vdrapi}" 'wget')
optdepends=('vdr-scraper2vdr: To display scraped images in various EPG views (requires epgd)'
            'vdr-tvscraper-git: To display scraped images in various EPG views')
conflicts=(skindesigner-{blackhole,holo,metrixhd,nopacity,shady,shady_kiss,skinelchi,tryouts})
replaces=(skindesigner-{blackhole,holo,metrixhd,nopacity,shady,shady_kiss,skinelchi,tryouts})
_plugname='skindesigner'

source=("${_gitname}::git+git://projects.vdr-developer.org/vdr-plugin-skindesigner.git#branch=master"
        'vdr-skindesigner-RU-locale.patch'
        '99-skindesigner.conf'
        "50-$_plugname.conf")

backup=("etc/vdr/conf.avail/50-$_plugname.conf")

options=('!emptydirs')

md5sums=('SKIP'
         'a7a11e176e93a1c4c4bfcb9a94966090'
         'bab4227c649d778e4b05483535afda0b'
         'c2090a1bdaddc7cd7c6f0cd7de553e65')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" PREFIX='/usr' install

    install -Dm644 "$srcdir/99-skindesigner.conf" "$pkgdir/etc/fonts/conf.avail/99-skindesigner.conf"
    install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"

    cd "$pkgdir/usr/lib/vdr/plugins/skindesigner/scripts"
    mv temperatures.default temperatures
    rm README temperatures.*
}

