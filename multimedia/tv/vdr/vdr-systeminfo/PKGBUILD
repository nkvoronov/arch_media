# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-systeminfo
pkgver=0.1.4
_vdrapi=2.4.0
pkgrel=1
pkgdesc="displays system informations like CPU type and speed, disk and swap space etc."
url="http://firefly.vdr-developer.org/systeminfo/index.html"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('iproute2' 'lm_sensors' 'hddtemp' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}

source=("http://firefly.vdr-developer.org/systeminfo/${pkgname}-${pkgver}.tar.bz2"
        'vdr-systeminfo-01_scriptpath.patch'
        'vdr-systeminfo-02_RU-locale.patch'
        'systeminfo.sh'
        "plugin.${_plugname}.conf")

md5sums=('6bf8a7d9def1d16b0819c755f507e9cd'
         'a425f70ba7a2342f9dc6c248b5a3d9fa'
         'a2537f30d24d8d105ee7037be9edc900'
         'ea31a11d19b78f30f389793ebc34c3cb'
         '1612b322796771bb8c10d14528bb4119')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

prepare() {
    cd "${srcdir}/${_plugname}-${pkgver}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_plugname}-${pkgver}"
    make
}

package() {
    cd "${srcdir}/${_plugname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -Dm755 "${srcdir}/systeminfo.sh" "$pkgdir/usr/share/vdr/plugins/systeminfo/systeminfo.sh"
    install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
