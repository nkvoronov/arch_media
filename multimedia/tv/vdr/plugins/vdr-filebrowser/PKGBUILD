pkgname=vdr-filebrowser
pkgver=0.2.1
_vdrapi=2.4.0
pkgrel=20
pkgdesc="filebrowser with custom commands for vdr"
url="http://opensource.holgerbrunn.net/vdr/filebrowser/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL3')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
replaces=("vdr-plugin-$_plugname")
conflicts=("vdr-plugin-$_plugname")
source=("http://opensource.holgerbrunn.net/vdr/filebrowser/$pkgname-$pkgver.tgz"
        "filebrowser-makefile-1.7.36.tar.gz::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=32857"
        'vdr-plugin-filebrowser-02_RU-locale.patch')
backup=("etc/vdr/plugins/filebrowser/commands.conf"
        "etc/vdr/plugins/filebrowser/order.conf"
        "etc/vdr/plugins/filebrowser/othercommands.conf"
        "etc/vdr/plugins/filebrowser/sources.conf"
        "etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('a1813fec39604b62c38c3ab44fe4aa5f'
         'd6bd0a727bb17e01a029391ec93e3630'
         'ff53885149826866ee26289c58730e76')

prepare() {
  cd "${srcdir}/$_plugname-${pkgver}"
  cp "$srcdir/filebrowser-makefile-1.7.36" Makefile
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/$_plugname-${pkgver}"
  make
}

package() {
  cd "${srcdir}/$_plugname-${pkgver}"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/etc/vdr/plugins/filebrowser"
  install -m644 examples/filebrowser/*  "$pkgdir/etc/vdr/plugins/filebrowser"
  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
