# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-skinflatplus-git
_gitname='vdr-skinflatplus'
_verpkg='0.6.0'
pkgver=0.6.0_g5a8c1819
_vdrapi=2.2.0
pkgrel=1
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflatplus"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname='skinflatplus'

source=("${_gitname}::git+git://projects.vdr-developer.org/skin-flatplus.git#branch=master"
        'vdr-skinflatplus-makefile.patch'
        'vdr-skinflatplus-fixsource.patch'
        'vdr-skinflatplus-RU-locale.patch'
        "50-$_plugname.conf")

backup=('etc/epgd/eventsview-flatplus.sql'
        "etc/vdr/conf.avail/50-$_plugname.conf"
        'etc/vdr/plugins/skinflatplus/configs/'{MV_default,default,fnu_default})

md5sums=('SKIP'
         '1f554e17285948f904382d62277f8f45'
         'a57bb0a835c7956fb7ab68618bf28860'
         '0eec60cfddbf2f387ba8b87f28b51cff'
         'fe042f86924c399dd445f6366fc94a2d')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 contrib/eventsview-flatplus.sql "$pkgdir/etc/epgd/eventsview-flatplus.sql"
    install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
