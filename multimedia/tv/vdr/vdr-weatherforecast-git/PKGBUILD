# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-weatherforecast-git
_gitname='vdr-weatherforecast'
_verpkg='0.2.0'
pkgver=0.2.0_g842b086
_vdrapi=2.4.0
pkgrel=1
pkgdesc="provides a weather forecast based on forecast.io data"
url="http://projects.vdr-developer.org/projects/plg-weatherforecast"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('curl' 'jansson' "vdr-api=$_vdrapi" 'vdr-skindesigner-git')
_plugname='weatherforecast'

source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-weatherforecast.git#branch=master"
       'vdr-weatherforecast-02_RU-locale.patch')

md5sums=('SKIP'
         '978b4cea7eb8b03c5875a50874abec3b')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    mkdir -p "$pkgdir/etc/vdr/conf.avail"
    echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
