pkgname=vdr-lcdproc
pkgver=0.0.10_jw9_g0c54897
_gitname='source-git'
_verpkg='0.0.10_jw9'
_vdrapi=2.4.0
pkgrel=20
pkgdesc="Output to LCD modules that are supported by LCDproc"
url="http://projects.vdr-developer.org/projects/plg-lcdproc"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
optdepends=('lcdproc: to use local displays')
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/vdr-projects/vdr-plugin-lcdproc.git#branch=master"
        "50-$_plugname.conf"
        '92-lcdproc.rules'
        'vdr-plugin-lcdproc-02_dynamite.patch'
        'vdr-plugin-lcdproc-03_vdr-2.3.2-v2.patch'
        'vdr-plugin-lcdproc-04_RU-locale.patch')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         '0221e0b8878d56e3a057ac37a9c853cf'
         '67d1e0de8e90e0d4e9e977ddfd952442'
         '113046cc762f72d8690e465e901309c7'
         '047746f6f729704ec93da66736480c6e'
         'e47d19483cef51b124e4e4ac588b0dc1')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/92-lcdproc.rules" "$pkgdir/usr/lib/udev/rules.d/92-lcdproc.rules"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
