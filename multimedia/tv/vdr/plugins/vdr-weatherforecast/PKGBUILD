pkgname=vdr-weatherforecast
pkgver=0.2.0_g842b086
_gitname='source-git'
_verpkg='0.2.0'
_vdrapi=2.4.0
pkgrel=20
pkgdesc="provides a weather forecast based on forecast.io data"
url="http://projects.vdr-developer.org/projects/plg-weatherforecast"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('curl' 'jansson' "vdr-api=$_vdrapi" 'vdr-skindesigner')
_plugname=${pkgname//vdr-/}
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
source=("${_gitname}::git+https://github.com/vdr-projects/vdr-plugin-weatherforecast.git#branch=master"
        'vdr-plugin-weatherforecast-02_RU-locale.patch')
md5sums=('SKIP'
         '978b4cea7eb8b03c5875a50874abec3b')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
