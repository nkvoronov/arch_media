# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-sleeptimer-git
_gitname='vdr-sleeptimer'
_verpkg='0.8.3'
pkgver=0.8.3_gb777501
_vdrapi=2.4.0
pkgrel=1
pkgdesc="Shutdown, mute or execute a custom command after a configurable timespan"
url="http://projects.vdr-developer.org/projects/plg-sleeptimer"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname='sleeptimer'

source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-sleeptimer.git#branch=master"
        'vdr-sleeptimer-02_RU-locale.patch'
        "plugin.${_plugname}.conf")

md5sums=('SKIP'
         '455a2355678f7378b6282e39dc9dd00a'
         '2e3b79cb39484d0393935edc4337ea93')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
