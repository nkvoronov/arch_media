# Maintainer: dhead666 <myfoolishgames@gmail.com>
# https://github.com/dhead666/archlinux-pkgbuilds
#
# package version generated with # git log -1 --date=short --format="%cd.%h" | tr -d '-'

pkgname=kodi-addon-adsp-freesurround-git
_gitname='kodi-addon-adsp-freesurround'
_verpkg='0.2.1'
pkgver=0.2.1_g34f5040
pkgrel=1
_branch=Krypton
pkgdesc='FreeSurround ADSP addon for Kodi'
arch=('i686' 'x86_64')
url="https://github.com/kodi-adsp/adsp.freesurround"
license=('GPL')
groups=('kodi')
makedepends=('cmake' 'git' 'kodi-platform-git')
depends=('kodi-git')
#conflicts=('')
#replaces=('')
source=("${_gitname}::git+https://github.com/kodi-adsp/adsp.freesurround.git#branch=${_branch}")
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

build() {
    cd "${srcdir}/${_gitname}"
  
    rm -rf build
    mkdir build
    cd build
    
    cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..
    make
}

package() {
    cd "${srcdir}/${_gitname}/build"
    make DESTDIR="$pkgdir" install
}
