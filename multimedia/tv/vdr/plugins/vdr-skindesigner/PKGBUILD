pkgname='vdr-skindesigner'
pkgver=1.2.7_ga685cbb
_gitname='source-git'
_verpkg='1.2.7'
_vdrapi=2.4.0
pkgrel=1
pkgdesc="Highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('git' 'librsvg' 'ttf-opensans' 'unzip' "vdr-api=${_vdrapi}" 'wget')
optdepends=('vdr-scraper2vdr: To display scraped images in various EPG views (requires epgd)'
            'vdr-tvscraper: To display scraped images in various EPG views')
conflicts=(skindesigner-{blackhole,holo,metrixhd,nopacity,shady,shady_kiss,skinelchi,tryouts})
replaces=(skindesigner-{blackhole,holo,metrixhd,nopacity,shady,shady_kiss,skinelchi,tryouts})
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/vdr-projects/vdr-plugin-skindesigner.git#branch=master"
        '99-skindesigner.conf'
        'vdr-plugin-skindesigner-02_RU-locale.patch'
        "50-$_plugname.conf")
install="$pkgname.install"
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
options=('!emptydirs')
md5sums=('SKIP'
         'bab4227c649d778e4b05483535afda0b'
         'a7a11e176e93a1c4c4bfcb9a94966090'
         'ebc6537bf0d3e604c59884ec408f7e9c')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" PREFIX='/usr' install
  mkdir -p "$pkgdir/etc/vdr/plugins/skindesigner/"
  ln -s /usr/share/vdr/plugins/skindesigner/dtd "$pkgdir/etc/vdr/plugins/skindesigner/dtd"
  install -Dm644 "$srcdir/99-skindesigner.conf" "$pkgdir/etc/fonts/conf.avail/99-skindesigner.conf"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
  cd "$pkgdir/usr/lib/vdr/plugins/skindesigner/scripts"
  mv temperatures.default temperatures
  rm README temperatures.*
}
