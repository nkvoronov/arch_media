pkgname=vdr-sleeptimer
_gitname='source-git'
_verpkg='0.6.0'
pkgver=0.6.0_gb777501
_vdrapi=2.4.0
pkgrel=20
pkgdesc="Shutdown, mute or execute a custom command after a configurable timespan"
url="http://projects.vdr-developer.org/projects/plg-sleeptimer"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/vdr-projects/vdr-plugin-sleeptimer.git#branch=master"
        'vdr-plugin-sleeptimer-02_RU-locale.patch'
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         '455a2355678f7378b6282e39dc9dd00a'
         '7ac2bda3d2a6238e815a6b04bccf4a81')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
