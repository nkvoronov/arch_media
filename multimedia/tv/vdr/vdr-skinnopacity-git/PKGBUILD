# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-skinnopacity-git
_gitname='vdr-skinnopacity'
_verpkg='1.1.3'
pkgver=1.1.3_gf593ad5
_vdrapi=2.4.0
pkgrel=1
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflatplus"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname='skinnopacity'

source=("${_gitname}::git+git://github.com/vdr-projects/vdr-plugin-skinnopacity.git#branch=master"
        'vdr-skinnopacity-02_vdr-2.3.3-compat.patch'
        'vdr-skinnopacity-03_vdr-2.3.5-compat.patch'
        'vdr-skinnopacity-04_vdr-2.3.8-compat.patch'
        'vdr-skinnopacity-05_RU-locale.patch'
        "plugin.${_plugname}.conf")

md5sums=('SKIP'
         '63c2a5fbf75d0dceb6d3bcf47195834e'
         'df5faf256116d644c572e5e1b9e03ef3'
         'bfbb877e46bb66ed9b508226cea5ad8b'
         '7c1ffdea059d6e26a3157c48a78e5539'
         'd9cbf3c31f08be59f8fe840cc2c81cb3')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf"
        'etc/vdr/plugins/skinnopacity/themeconfigs/theme-darkredNG.conf'
        'etc/vdr/plugins/skinnopacity/themeconfigs/theme-default.conf'
        'etc/vdr/plugins/skinnopacity/themeconfigs/theme-iceblue.conf'
        'etc/vdr/plugins/skinnopacity/themeconfigs/theme-light.conf')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    sed -i 's/Magick++/GraphicsMagick++/g' Makefile
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
