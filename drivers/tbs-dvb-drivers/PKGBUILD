# Maintainer:         Alexander Blinne "Sunday" <alexander at blinne dot net>
# Original Submitter: Wessel Dirksen "p-we" <wdirksen at gmail dot com>

pkgname=tbs-dvb-drivers
pkgver=v170330_4.12.8_2_ARCH
pkgrel=1
pkgdesc="TBS proprietary DVB drivers + firmware"
url="http://www.tbsdtv.com"
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('linux-headers' 'wget' 'moreutils' 'coreutils')
optdepends=('linuxtv-dvb-apps: handy DVB tools' 'v4l-utils: hardware support for some cards')
conflicts=('ffdecsawrapper' 'tbs-linux-drivers' 'tbs-dvb-drivers' 'media-build-git' 'tbs-linux-drivers-cc-git')
provides=('tbs-dvb-drivers')
install='tbs-dvb-drivers.install'

_tbsver=v170330

source=("http://www.tbsdtv.com/download/document/common/tbs-linux-drivers_$_tbsver.zip"
        'tbs-linux-drivers_imon_fix.patch'
        'tbs-linux-drivers_omicom_s2_support.patch'
        'tbs-dvb-drivers.install')

md5sums=('5b703b519b0e6a167b8bfb9627afda35'
         '3314b78f4fa085b337c7fd4fda447251'
         '16c50c4a2f422e6f8dbe39f3a02aaf08'
         '59f8329e80cbcb94235eda483983efc8')

pkgver() {
  _kernel=`uname -r | sed -r 's/-/_/g'`
  echo "$_tbsver"_"$_kernel"
}

prepare() {
  cd $srcdir
  tar xjf linux-tbs-drivers.tar.bz2
  chmod -R 777 $srcdir/linux-tbs-drivers
  cd $srcdir/linux-tbs-drivers
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd $srcdir/linux-tbs-drivers
  make distclean
  if [ `uname -m` == "x86_64" ]; then
    ./v4l/tbs-x86_64.sh
  else
    ./v4l/tbs-x86_r3.sh
  fi
  if [ -z "$_kernel" ]
  then
    _kernel=`uname -r`
  fi
  make
}

package() {
  mkdir -p $pkgdir/usr/lib/modules/`uname -r`/updates/tbs
  mkdir -p $pkgdir/usr/lib/firmware

  install -m0644 $srcdir/*dvb*.fw  $pkgdir/usr/lib/firmware
  find "$srcdir/linux-tbs-drivers" -name '*.ko' -exec cp {} $pkgdir/usr/lib/modules/`uname -r`/updates/tbs \;

  echo ""
  msg "Compressing modules, this will take awhile..."
  echo ""
  find "$pkgdir" -name '*.ko' -print0 | xargs -0 -P`nproc` -n10 gzip -9

  chmod -R go-w $pkgdir/usr/lib/modules/`uname -r`/updates
}
