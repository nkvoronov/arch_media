# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr
pkgver=2.4.0
pkgrel=2
pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder"
url="http://tvdr.de/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('libcap' 'libjpeg-turbo' 'libsystemd' 'perl' 'ttf-font')
makedepends=('systemd') #libsystemd should be enough but the pkg-config file is missing in the libsystemd package
optdepends=('lirc-utils: remote control support')
replaces=('runvdr-extreme')
conflicts=('runvdr-extreme')
provides=("vdr-api=2.4.0")
install='vdr.install'

source=("ftp://ftp.tvdr.de/vdr/${pkgname}-${pkgver}.tar.bz2"
        'vdr-01-fix-svdrp-modt-recflag.patch'
        'vdr-02_newplugin.patch'
        'vdr-03_makeconfig.patch'
        'vdr-04_ncursesw-include.patch'
        'vdr-05_menuorg.patch'
        'vdr-06_mainmenuhooks-v1.0.1.patch'
        'vdr-07_eventdetails-v3.patch'
        'vdr-08_dynamite.patch'
        'vdr-09_pin.patch'
        'vdr-10_hide-first-recording-level-v2.patch'
        'vdr-11_missing-plugin.patch'
        'vdr-12_patch-for-permashift.patch'
        'vdr-13_decrease-channels.conf-autosave-delay.patch'
        'channels.conf'
        'diseqc.conf'
        'keymacros.conf'
        'scr.conf'
        'sources.conf'
        'setup.conf'
        'svdrphosts.conf'
        'sttng-blue.theme'
        'sttng-cool.theme'
        'vdr.conf' 
        '60-create-dvb-device-units.rules'
        'gen-sddropin'
        'shutdown.sh'
        'shutdown-wrapper.c'
        'vdr.service'
        'vdr.sysuser')

md5sums=('12c6a3abeadfa915fcfe736bb047a3ab'
         '1a0a6c72b88b453dd5184b95bd948f7e'
         '588683d61d591ea5dbeebb3b0633ae3a'
         '72a1dece684057107f6592c65838d7e2'
         'bf4fe165b489f0814b6759b4086ab937'
         '009e84e7b8f0339bd0737955a850653e'
         'a395e7ccb7fce77e853aedebd59b7e07'
         '67eb04e03b494ae977059feb9e25908f'
         '95790886150144742f43c2f9d907be62'
         '90458ef8ac9029ed403b3ad2b0388ba2'
         'ec14d86bab1bee528a70c02bf0563d7d'
         '4dcd9323530dcd19f45eea608fb34955'
         'dd1faa2db33ca2b3273f79e6af12961e'
         '0fb52a3c3cf25bf25563de4bdb8372b9'
         '0e23e969521426065c731447deca047e'
         '11e8564f2e8aee4aed052729dcc4cfa2'
         '67a3fa01da962a80b1e8979d1d2aceb9'
         '4a17ad89d2cab918cbec063f3a5a2e08'
         '6f6373851711b38d341342c5f68415bb'
         '883d5831309ef5730ca444c32f0cc8f3'
         '252f767cbd8cfff5518bf337022adeb3'
         '889a665836642facf66ed917c1c9e72b'
         '7d0489f6999377669598fbe2ab00865e'
         'd6994144ec58732b869222be68880b95'
         '23d6e1ca0a36cfdbd35d3b1a61f0a105'
         '3565ca5ad9be5c75f66478f0796b120d'
         'dd20f932b846b5f50ac455b65e9432ad'
         '7cad811b4ac5ee6c0b5496d006f1e0ee'
         '1b81ba1063c3da92efd94b6251cdf65a'
         '59ce04d1d01bf92bf6cfc0b74223191c')

backup=('etc/vdr/channels.conf'
        'etc/vdr/diseqc.conf'
        'etc/vdr/keymacros.conf'
        'etc/vdr/scr.conf'
        'etc/vdr/sources.conf'
        'etc/vdr/svdrphosts.conf')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    #echo 'CFLAGS      += -O3' > Make.config
    #echo 'CXXFLAGS    += -O3' >> Make.config
    #echo 'PREFIX       = /usr' >> Make.config
    #echo 'RESDIR       =/usr/share/vdr' >> Make.config
    #echo 'CONFDIR      =/etc/vdr' >> Make.config
    #echo 'CACHEDIR     =/var/cache/vdr' >> Make.config
    #echo 'VIDEODIR     =/var/lib/video.00' >> Make.config
    #echo 'LIBDIR       = /usr/lib/vdr/plugins' >> Make.config
    #echo 'VDR_USER     = vdr' >> Make.config
    #echo 'SDNOTIFY     = 1' >> Make.config
    #echo 'LIRC_DEVICE  = /run/lirc/lircd' >> Make.config

    sed -i 's/libsystemd-daemon/libsystemd/g' Makefile

    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done

    rm_plugins="$(ls PLUGINS/src/ | grep -vE '(dvbhddevice|dvbsddevice)')"
    for i in $rm_plugins; do rm -rf "PLUGINS/src/$i"; done
}

build() {
    gcc -o shutdown-wrapper shutdown-wrapper.c

    cd "${srcdir}/${pkgname}-${pkgver}"
    make
}

package() {
    mkdir -p "$pkgdir/usr/include/vdr"
    install -Dm644 "${srcdir}/${pkgname}-${pkgver}/Make.config" "$pkgdir/usr/include/vdr"

    mkdir -p "$pkgdir/etc/vdr"

    install -Dm644 "$srcdir/svdrphosts.conf" "$pkgdir/etc/vdr"
    install -Dm644 "$srcdir/channels.conf" "$pkgdir/etc/vdr"
    install -Dm644 "$srcdir/diseqc.conf" "$pkgdir/etc/vdr"
    install -Dm644 "$srcdir/setup.conf" "$pkgdir/etc/vdr"

    mkdir -p "$pkgdir/etc/vdr/themes"

    install -Dm644 "$srcdir/sttng-blue.theme" "$pkgdir/etc/vdr/themes"
    install -Dm644 "$srcdir/sttng-cool.theme" "$pkgdir/etc/vdr/themes"

    mkdir -p "$pkgdir/etc/vdr/plugins"

    install -Dm644 "$srcdir/vdr.conf" "$pkgdir/etc/vdr/conf.d/vdr.conf"
    install -Dm644 "$srcdir/60-create-dvb-device-units.rules" "$pkgdir/usr/lib/udev/rules.d/60-create-dvb-device-units.rules"
    install -Dm644 "$srcdir/vdr.service" "$pkgdir/usr/lib/systemd/system/vdr.service"

    install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
    chgrp 666 "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
    chmod u+s "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"

    install -Dm755 "$srcdir/gen-sddropin" "$pkgdir/usr/bin/vdr-gensddropin"
    install -Dm755 "$srcdir/shutdown.sh" "$pkgdir/usr/lib/vdr/bin/shutdown.sh"

    #Install sysuser config
    install -Dm644 ${srcdir}/$pkgname.sysuser "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p "$pkgdir/usr/share/vdr"
    make DESTDIR="${pkgdir}" install

    mkdir -p "$pkgdir/usr/share/vdr/shutdown-hooks"
    mkdir -p "$pkgdir/var/lib/video.00"

    chown -R 666:666 "$pkgdir/etc/vdr"
    chown -R 666:666 "$pkgdir/var/cache/vdr"
    chown -R 666:666 "$pkgdir/var/lib/video.00"
}
