pkgname=kodi-platform-git
_gitname='source-git'
pkgver=r57.e8574b8
pkgrel=1
pkgdesc='Kodi platform support library'
arch=('armv7h' 'i686' 'x86_64')
url="https://github.com/xbmc/kodi-platform"
license=('GPL')
makedepends=('cmake' 'git')
depends=('kodi')
provides=('kodi-platform')
conflicts=('kodi-platform')
source=("${_gitname}::git+https://github.com/xbmc/kodi-platform.git")
md5sums=('SKIP')
API=99

# Corresponding commit IDs are taken from
# https://github.com/xbmc/xbmc/blob/$BRANCH/project/cmake/addons/depends/common/kodi-platform/kodi-platform.txt
case "$API" in
  15)  depends+=('libplatform-legacy')
       source[0]="${_gitname}::git+https://github.com/xbmc/kodi-platform.git#commit=054a42f66" ;;
  16)  depends+=('libplatform-legacy')
       source[0]="${_gitname}::git+https://github.com/xbmc/kodi-platform.git#commit=15edaf78d" ;;
  17)  depends+=('p8-platform')
       makedepends+=('kodi-dev-git')
       source[0]="${_gitname}::git+https://github.com/xbmc/kodi-platform.git#commit=c8188d826" ;;
  99)  depends+=('p8-platform')
       makedepends+=('kodi-dev-git') ;;
  *)   error "Unknown API version. Follow instructions in PKGBUILD." && false
esac

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  rm -rf build
  mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBDIR_NOARCH=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_PREFIX_PATH=/usr/lib/kodi \
    ..
  make
}

package() {
  cd "${srcdir}/${_gitname}/build"
  make DESTDIR="$pkgdir" install
}
