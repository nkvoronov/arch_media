# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-wirbelscan
pkgver=2017.06.04
_vdrapi=2.4.0
pkgrel=1
pkgdesc="Performs a channel scans for DVB-T, DVB-C and DVB-S"
url="http://wirbel.htpc-forum.de/wirbelscan/index2.html"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}

source=("http://wirbel.htpc-forum.de/wirbelscan/vdr-wirbelscan-dev-$pkgver.tgz"
        'vdr-wirbelscan-01_makefile.patch'
        "plugin.${_plugname}.conf")

md5sums=('4fd1df45997bad5dce90db10665b860c'
         'febdd0289ea2d53838774da9a1894bcd'
         '80776f2f065413d671c834771e484a48')

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  #cp backup/Makefile.old Makefile
  #sed -i '/Make.global/d' Makefile
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       all

  install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
