pkgname=kodi-addon-inputstream-rtmp-git
_gitname='source-git'
_verpkg='3.4.0'
pkgver=3.4.0_g8505b6a
pkgrel=1
pkgdesc="librtmp support for Kodi 18+"
arch=('x86_64' 'i686' 'armv7h')
url="https://github.com/xbmc/inputstream.rtmp"
license=('GPL2')
depends=('kodi-devel' 'rtmpdump' 'p8-platform')
makedepends=('kodi-devel-dev' 'cmake' 'git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${_gitname}::git+https://github.com/xbmc/inputstream.rtmp.git#branch=Matrix")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

build() {
  cd "$srcdir/${_gitname}"
  mkdir -p build && pushd build
  cmake .. -DCMAKE_INSTALL_PREFIX="/usr"
  make
  popd
}

package() {
  cd "$srcdir/${_gitname}/build"
  make DESTDIR="$pkgdir/" install
}
