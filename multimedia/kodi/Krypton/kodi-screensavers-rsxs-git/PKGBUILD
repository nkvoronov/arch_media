pkgname=kodi-screensavers-rsxs-git
_gitname='source-git'
_verpkg='1.3.0'
pkgver=1.3.0_g781f76f
pkgrel=1
_commit=781f76f
_branch=master
pkgdesc='RSXS screensavers addon for Kodi'
arch=('i686' 'x86_64')
url='https://github.com/notspiff/screensavers.rsxs'
license=('GPL')
groups=('kodi')
makedepends=('cmake')
depends=('kodi' 'libpng')
source=(
  "${_gitname}::git+https://github.com/notspiff/screensavers.rsxs.git#commit=${_commit}"
  '0001-fix-gcc5-compile.patch'
  '0002-fix-default-include-dir.patch')
md5sums=(
  'SKIP'
  '3c0fc8c71c9ed7c76d4809a76b654283'
  'f32a6f97865027503a002f4dc72045b6')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
  cd "${srcdir}/${_gitname}"

  rm -rf build
  mkdir build
  cd build

  cmake \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=Release \
  ..
  make
}

package() {
  cd "${srcdir}/${_gitname}/build"
  make DESTDIR="$pkgdir" install
}
