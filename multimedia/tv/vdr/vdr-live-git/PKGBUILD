# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-live-git
_gitname='vdr-live'
_verpkg='2.3.1'
pkgver=2.3.1_ge582514
_vdrapi=2.2.0
pkgrel=1
pkgdesc="Adds the possibility to control VDR and some of it's plugins by a web interface."
url="http://projects.vdr-developer.org/projects/plg-live"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('pcre' 'tntnet' "vdr-api=${_vdrapi}")
optdepends=('vdr-epgsearch-git: Manage searchtimers'
            'vdr-streamdev-git: Stream live TV')
makedepends=('git')
install="vdr-live.install"
_plugname='live'

source=("${_gitname}::git+git://projects.vdr-developer.org/vdr-plugin-live.git#branch=master"
        'vdr-live-RU-locale.patch'
        "50-$_plugname.conf")

backup=("etc/vdr/conf.avail/50-$_plugname.conf")

md5sums=('SKIP'
         '568f95b5540be02e93cf5e853fddbe9e'
         'eed5de703ea95782878ce4c28bb43c74')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"

    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done

    # Use the more modern ResourceDirectory in /usr/share instead of /var/lib
    sed -i 's/cPlugin::ConfigDirectory/cPlugin::ResourceDirectory/g' live.cpp
}

build() {
    cd "${srcdir}/${_gitname}"
    make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="$pkgdir" install

    # Copy resource files
    mkdir -p "$pkgdir/usr/share/vdr/plugins"
    cp -r live "$pkgdir/usr/share/vdr/plugins"

    install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
