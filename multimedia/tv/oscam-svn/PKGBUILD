pkgname=oscam-svn
pkgver=11425
pkgrel=1
_svnname='source-svn'
pkgdesc="The Open Source Conditional Access Module daemon"
url="http://oscam.to/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('bash' 'openssl' 'libusbx' 'pcsclite')
makedepends=('subversion')
provides=('oscam')
conflicts=('oscam')
install=oscam.install
source=("${_svnname}::svn+http://www.streamboard.tv/svn/oscam/trunk"
  'oscam.conf'
  'oscam.service')
md5sums=('SKIP'
  '3c732911da0c9ff8968371142a9a6a9e'
  '49d62e28450a71930497d5a53ff4f147')

pkgver() {
  cd "$SRCDEST/${_svnname}"
  svnversion
}

build() {
  cd "${srcdir}/${_svnname}"
  make allyesconfig
  ./config.sh --disable WITH_SSL
  make CONF_DIR=/etc/oscam \
       USE_LIBUSB=1 \
       USE_PCSC=1 \
       OSCAM_BIN=oscam
}

package() {
  cd "${srcdir}/${_svnname}"

  install -D -m 755 "oscam" "$pkgdir/usr/bin/oscam"
  install -D -m 644 "$srcdir/oscam.conf" "$pkgdir/etc/conf.d/oscam"
  install -D -m 644 "$srcdir/oscam.service" "$pkgdir/usr/lib/systemd/system/oscam.service"
  install -m 755 -d "$pkgdir/etc/oscam"
  install -m 755 -d "$pkgdir/usr/share/doc/oscam"/{example,monitor}
  install -m 644 "Distribution/doc/txt"/* "${pkgdir}/usr/share/doc/oscam/"
  install -m 644 "Distribution/doc/example"/* "${pkgdir}/usr/share/doc/oscam/example"
  install -m 644 "Distribution/monitor"/* "${pkgdir}/usr/share/doc/oscam/monitor"
  install -m 755 -d "$pkgdir/usr/share/man"/{man1,man5}
  install -m 644 "Distribution/doc/man"/*.1 "${pkgdir}/usr/share/man/man1"
  install -m 644 "Distribution/doc/man"/*.5 "${pkgdir}/usr/share/man/man5"
}
