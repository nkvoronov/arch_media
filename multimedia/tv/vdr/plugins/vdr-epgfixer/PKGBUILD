pkgname=vdr-epgfixer
pkgver=0.3.1_g354f28b
_verpkg=0.3.1
_gitname='source-git'
_vdrapi=2.4.0
pkgrel=20
pkgdesc="VDR plugin for doing extra fixing of EPG data"
url="http://projects.vdr-developer.org/projects/plg-epgfixer"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' 'pcre' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=${pkgname//vdr-/}
source=("${_gitname}::git+https://github.com/vdr-projects/vdr-plugin-epgfixer.git#branch=master"
        'vdr-plugin-epgfixer-02_RU-locale.patch')
backup=("etc/vdr/conf.avail/50-$_plugname.conf"
        'etc/vdr/plugins/epgfixer/blacklist.conf'
        'etc/vdr/plugins/epgfixer/charset.conf'
        'etc/vdr/plugins/epgfixer/epgclone.conf'
        'etc/vdr/plugins/epgfixer/regexp.conf')
md5sums=('SKIP'
         '747f8537ba24aac066ebc3b625bf3478')

pkgver() {
  cd "${srcdir}/${_gitname}"
  _git=`git rev-parse --short HEAD`
  printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
