# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-dvbapi-git
_gitname='vdr-dvbapi'
_verpkg='2.2.4'
pkgver=2.2.4_g7d51cc4
_vdrapi=2.4.0
pkgrel=1
pkgdesc="A bridge between VDR and OScam."
url="https://github.com/manio/vdr-plugin-dvbapi"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' 'libdvbcsa' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname='dvbapi'

source=("${_gitname}::git+git://github.com/manio/vdr-plugin-dvbapi.git#branch=master"
    'vdr-dvbapi-RU-locale.patch'
    "plugin.${_plugname}.conf")

backup=("etc/vdr/conf.avail/plugin.${_plugname}.conf")

md5sums=('SKIP'
         'ea6908f51cc42d42f0a4a249fe374f89'
         'd6ca9581dfda75a19589650f86cf596a')

pkgver() {
    cd "${srcdir}/${_gitname}"
    _git=`git rev-parse --short HEAD`
    printf "%s_%s" "$_verpkg" "g$_git"
}

prepare() {
    cd "${srcdir}/${_gitname}"
    for f in "$srcdir"/*.patch; do patch -p1 < "$f"; done
}

build() {
    cd "${srcdir}/${_gitname}"
    sed -i 's/ -fdiagnostics-color=auto//g' Makefile
    make LIBDVBCSA=1
}

package() {
    cd "${srcdir}/${_gitname}"
    make LIBDVBCSA=1 DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/plugin.${_plugname}.conf" "$pkgdir/etc/vdr/conf.avail/plugin.${_plugname}.conf"
}
