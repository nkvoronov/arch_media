pkgname=emby-server-git
pkgver=3.5.2.0
pkgrel=1
pkgdesc='Bring together your videos, music, photos, and live television'
arch=('any')
url='http://emby.media'
license=('custom')
depends=('dotnet-runtime' 'ffmpeg' 'skia-sharp60' 'sqlite')
conflicts=('emby-server' 'emby-server-beta-git' 'emby-server-dev-git')
install='emby-server.install'
source=("https://github.com/MediaBrowser/Emby.Releases/releases/download/${pkgver}/embyserver-netcore_${pkgver}.zip"
        'emby-server'
        'emby-migrate-database'
        'emby-server.conf'
        'emby-server.service'
        'emby-server.sysusers'
        'emby-server.tmpfiles'
        'license.docx')
backup=('etc/conf.d/emby-server')
sha256sums=('fe5561c27a5d2bbe79a9717ecb2eb8ce4f243592494b813250d2db56da5a3710'
            'fcab0c95fb94179ca52d63b354b2ca44b8e35885469cb2b3d006b5fbc5622490'
            'b25bf83a0ab371aff3b13b82f7af71b51bfe6d7e51eb8a8a3dd8f0774ffce6a5'
            '5d14f999784df1a0fff89e5c35f9b36cd938d097a8e7037997774ef01c7c179a'
            'efdaabd3002fee13a6831e90fd700dea4d7c6c954ede00da6f28c1eada33ad8c'
            '9caba923f3673f3e1ba4c8b002253e5de462078ab8602ddaf3976125cdc9a172'
            '58121e34d260be790f3d00bef05bb43459c6f6cf15bc6ae680b3ebc442f842d6'
            'a6d7ea65dcb06392479a85e1a10a7aeb872d803da6f784f6935fcd4ee63008c6')

prepare() {
  rm -rf system/{electron,runtimes}
}

package() {
  install -dm 755 "${pkgdir}"/usr/lib
  cp -dr --no-preserve='ownership' system "${pkgdir}"/usr/lib/emby-server
  ln -s ../libSkiaSharp.so.60.0.0 "${pkgdir}"/usr/lib/emby-server/libSkiaSharp.so
  install -Dm 755 emby-server -t "${pkgdir}"/usr/bin/
  install -Dm 755 emby-migrate-database -t "${pkgdir}"/usr/bin/
  install -Dm 644 emby-server.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm 644 emby-server.sysusers "${pkgdir}"/usr/lib/sysusers.d/emby-server.conf
  install -Dm 644 emby-server.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/emby-server.conf
  install -Dm 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server
  install -Dm 644 license.docx -t "${pkgdir}"/usr/share/licenses/emby-server/
}

